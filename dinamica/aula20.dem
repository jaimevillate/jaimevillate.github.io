"Definição das duas funções"$
f1: 6*x2*(x2^2+x1^2-1)^2 - 3*x1^2*x2^2;
f2: 2*x1*x2^3 - 6*x1*(x2^2+x1^2-1)^2;

"Determinação dos pontos de equilíbrio"$
p: solve([f1,f2]);

"Há 13 pontos mas apenas 9 estão no plano real."$

"Para extrair os primeiros 7 elementos e os últimos 2, da lista de 13,
usa-se o comando:"$
p: append(rest(p,-6), rest(p,11));

"4 dos pontos estão nos eixos."$

"Cálculo da matriz jacobiana"$
J: jacobian([f1,f2],[x1,x2]);

"Cálculo das 9 matrizes das aproximações lineares"$
A: makelist(subst(q,J), q, p);

"Traços das 9 matrices"$
map(mat_trace, A);

"Todas têm traço nulo. De facto, o sistema é conservativo: a matriz
jacobiana tém traço nulo e, como tal, os pontos de equilíbrio
poderão ser ou centros ou pontos de sela."$

"Cálculo dos determinantes das 9 matrizes"$
map(determinant, A);

"O primeiro, oitavo e nono pontos são centros.
O sexto e sétimo são pontos de sela.
Nos 4 pontos nos eixos não há aproximação linear: esses pontos
não são dos tipos encontrados nos sistemas lineares."$

"Retrato de fase:"$
plotdf([f1,f2],[x1,x2],[x1,-2,2],[x2,-2,2]);
